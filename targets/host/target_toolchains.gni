# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pigweed/targets/host/target_toolchains.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_third_party/nanopb/nanopb.gni")

# Inherit from a Pigweed upstream toolchain and override backends as needed.
_host_toolchain_overrides = {
  # Configure backend for assert facade.
  pw_assert_BACKEND = "$dir_pw_assert_basic"

  # Configure backend for logging facade.

  # Plain text logging
  pw_log_BACKEND = "$dir_pw_log_basic"

  # Tokenized logging: https://pigweed.dev/pw_tokenizer/
  # pw_log_BACKEND = "$dir_pw_log_tokenized:log_backend"

  # Log handler backend
  # pw_tokenizer_GLOBAL_HANDLER_WITH_PAYLOAD_BACKEND =
  #     "//source/logging:tokenized_log_handler"
  # pw_tokenizer_GLOBAL_HANDLER_WITH_PAYLOAD_BACKEND =
  #     "//source/logging:hldc_log_handler"

  # Configure backend for pw_sys_io facade.
  pw_sys_io_BACKEND = "$dir_pw_sys_io_stdio"

  # Path to the nanopb installation. Defaults to included git module.
  dir_pw_third_party_nanopb = "//third_party/nanopb"
}

target_toolchain_host = {
  _excluded_members = [
    "defaults",
    "name",
  ]

  _excluded_defaults = [
    "pw_trace_BACKEND",
    "pw_trace_tokenizer_time",
  ]

  clang_debug = {
    name = "host_clang_debug"
    _toolchain_base = pw_target_toolchain_host.clang_debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*", _excluded_defaults)
      forward_variables_from(_host_toolchain_overrides, "*")
    }
  }

  # Toolchain for tests only.
  clang_debug_tests = {
    name = "host_clang_debug_tests"
    _toolchain_base = pw_target_toolchain_host.clang_debug
    forward_variables_from(_toolchain_base, "*", _excluded_members)
    defaults = {
      forward_variables_from(_toolchain_base.defaults, "*", _excluded_defaults)
      forward_variables_from(_host_toolchain_overrides, "*")

      # Force tests to use basic log backend to avoid generating and loading its
      # own tokenized database.
      pw_log_BACKEND = "$dir_pw_log_basic"
    }
  }
}

toolchains_list = [
  target_toolchain_host.clang_debug,
  target_toolchain_host.clang_debug_tests,
]
